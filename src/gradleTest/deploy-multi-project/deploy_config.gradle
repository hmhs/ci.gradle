allprojects {
  group = 'liberty.gradle'
  version = '1'

  ext {
    libertyVersion = project.property("libertyVersion")
    libertyDlRoot = project.property("liberty_dl_root")

    wlpServerName = 'libertyOnline'
  }

  apply plugin: 'java'

  repositories {
    mavenCentral()
  }

}

project(':app-ejb'){


// In this section you declare the dependencies for your production and test code
  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile group: 'org.eclipse.persistence', name: 'javax.persistence', version: '2.0.0'
    compile group: 'org.glassfish', name: 'javax.ejb', version: '3.1.1'

    testCompile group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.5.0'
    testCompile group: 'org.apache.derby', name: 'derby', version: '10.13.1.1'
  }
}

project(':app-war'){
  apply plugin: 'war'

// In this section you declare the dependencies for your production and test code
  dependencies {
    compile project(':app-ejb')

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
  }
}

project(':app-liberty'){
  apply plugin: 'liberty'

  dependencies {
    libertyDeploy project(":app-war")
  }

  liberty {
    install {
      runtimeUrl = "${libertyDlRoot}/wlp-webProfile7/${libertyVersion}/wlp-webProfile7-${libertyVersion}.zip"
    }

    server {
      name = wlpServerName
      bootstrapProperties = [
          'default.http.port':'9888',
          'default.https.port':'9443',
      ]
    }
  }
}

task ('runGradleTest') {
  dependsOn 'clean', ':app-war:build', ':app-liberty:deploy' //
  finalizedBy ':app-liberty:libertyStop'

  doLast {
    def file = new File(buildDir, "/wlp/usr/servers/${wlpServerName}/dropins/app-war-1.war")

    assert file.exists() : "file not found"
    assert file.canRead() : "file cannot be read"
  }
}